(defgeneric closurep (structure &optional operation)
  (:documentation "Checks if a given structure is closed under an operation."))
(defgeneric identityp (structure &optional operation)
  (:documentation "Checks if a given structure has an identity under an operation."))
(defgeneric invertiblep (structure &optional id operation)
  (:documentation "Checks if a given structure has an identity and every element is invertible under an operation."))

(defmethod closurep ((elements list) &optional operation)
  (loop for g in elements always
       (loop for h in elements always (and (member (funcall operation g h) elements :test #'equalp)
					   (member (funcall operation h g) elements :test #'equalp)))))
(defmethod closurep ((s set-class) &optional operation)
  (closurep (elements s) operation))
(defmethod closurep ((sg semi-group) &optional (operation (operation sg)))
  (call-next-method sg operation))

(defmethod invertiblep ((elements list) &optional id operation)
  (let ((identity (if id id (get-identity elements operation))))
    (loop for g in elements always
	 (get-inverse elements g identity operation))))
(defmethod invertiblep ((s set-class) &optional id operation)
  (invertiblep (elements s) id operation))
(defmethod invertiblep ((sg semi-group) &optional id (operation (operation sg)))
  (call-next-method sg id operation))

(defmethod identityp ((elements list) &optional operation)
  (when (get-identity elements operation) t))
(defmethod identityp ((s set-class) &optional operation)
  (identityp (elements s) operation))
(defmethod identityp ((sg semi-group) &optional (operation (operation sg)))
  (call-next-method sg operation))

(defgeneric group-p (stucture &optional operation)
  (:documentation "Checks if a given set and an operation forms a group."))
(defmethod group-p ((s set-class) &optional operation)
  (and (closurep s operation) (identityp s operation) (invertiblep s nil operation)))
(defmethod group-p ((sg semi-group) &optional (operation (operation sg)))
  (call-next-method sg operation))
